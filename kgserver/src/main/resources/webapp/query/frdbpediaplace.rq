#
# SPARQL Query 
# Extract a subgraph from dbpedia, to be processed by HTML Transformation st:navlab
#
# Olivier Corby - Wimmics INRIA I3S - 2014
#
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix o:    <http://dbpedia.org/ontology/>
prefix w:    <http://fr.wikipedia.org/wiki/>
prefix r:    <http://fr.dbpedia.org/resource/>
prefix p:    <http://fr.dbpedia.org/property/>

construct debug {
  ?r a o:Place 
  ?r rdfs:label ?label ; rdfs:comment ?com
  ?r o:wikipedia ?pr ; o:dbpedia ?r
  
  ?r ?q ?v . ?v rdfs:label ?lv      
  ?r o:thumbnail  ?ic
}
where {

service <http://fr.dbpedia.org/sparql> {
  
  ?r rdfs:label ?label . filter(lang(?label) = 'fr')
    
  optional { ?r rdfs:comment ?com . filter(lang(?com) = 'fr') }
	      
  optional { ?r o:thumbnail  ?ic  }
  
  optional {
    ?r ?q ?v . ?v rdfs:label ?lv 
    filter(lang(?lv) = 'fr')
    filter(
    ?q = p:sud || ?q = p:nord || ?q = p:est || ?q = p:ouest
    || ?q = p:sudEst || ?q = p:sudOuest
    || ?q = p:nordEst || ?q = p:nordOuest 

    )
  }
  
 
}


      
bind (concat(w:, (substr(?r, strlen(r:) + 1))) as ?pr)   
      
  
}
