#
# Compute the day of a date
# http://fr.wikipedia.org/wiki/DÃ©termination_du_jour
#
# Olivier Corby - Wimmics, Inria, I3S - 2015
#
prefix xt: <http://ns.inria.fr/sparql-extension/>
select 

(xt:jour("2015-06-06"^^xsd:date) as ?fr)
(xt:day("2015-06-06"^^xsd:date)  as ?en)

(package(xt:calendar,
define (xt:div(?a, ?b) = xsd:integer(floor(?a / ?b)))  ,
define (xt:mod(?a, ?b) = xsd:integer(?a - (?b * xt:div(?a, ?b))))  ,
define (xt:bisextile(?y) =
    ((xt:mod(?y, 4) = 0) &&
      ((xt:mod(?y, 100) != 0) || (xt:mod(?y, 400) = 0))))  ,
define (xt:ab(?y) = xt:div(?y, 100))  ,
define (xt:cd(?y) = xt:mod(?y, 100))  ,
define (xt:k(?y)  = xt:div(xt:cd(?y), 4)) ,
define (xt:q(?y)  = xt:div(xt:ab(?y), 4)) ,
define (xt:month(?m, ?y) =
  if (?m <= 2,
    if (xt:bisextile(?y),
      if (?m = 1, 3, 6),
      if (?m = 1, 4, 0)),
  if (?m in (3, 11), 0,
    if (?m in (6), 1,
      if (?m in (9, 12), 2,
	if (?m in (4, 7), 3,
	  if (?m in (10), 4,
	    if (?m in (5), 5, 6))))))))  ,
define (xt:find(?d) =
  let (?y = year(?d),
  let (?n = xt:k(?y) + xt:q(?y) + xt:cd(?y) +
       xt:month(month(?d), ?y) + day(?d) + 2 + 5 * xt:ab(?y),
  xt:mod(?n, 7))))  ,
define (xt:en(?n) =
  if (?n = 0, "Sunday",
  if (?n = 1, "Monday",
  if (?n = 2, "Tuesday",
  if (?n = 3, "Wednesday",
  if (?n = 4, "Thursday",
  if (?n = 5, "Friday",
  if (?n = 6, "Saturday"))))))))  ,
define (xt:fr(?n) =
  if (?n = 0, "Dimanche",
  if (?n = 1, "Lundi",
  if (?n = 2, "Mardi",
  if (?n = 3, "Mercredi",
  if (?n = 4, "Jeudi",
  if (?n = 5, "Vendredi",
  if (?n = 6, "Samedi"))))))))  ,
define (xt:day(?d)  = xt:en(xt:find(?d)))  ,
define (xt:jour(?d) = xt:fr(xt:find(?d)))
)
as ?def)
  

where  {}
