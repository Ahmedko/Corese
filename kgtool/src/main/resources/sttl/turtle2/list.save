#
# Turtle Pretty Printer
# Olivier Corby, Wimmics Inria I3S, 2013
#
template  {
  "(" 
    group { 
      if (?refer, st:turtle(?e), ?e) 
      ; separator = "\n"
    } 
  ")"
}
where {
  ?in rdf:first [] 
  filter st:isWFL(?in) 
  ?in rdf:rest* ?r . ?r rdf:first ?e
  filter (?r != rdf:nil && not exists { rdf:nil rdf:rest* ?r })
  
  bind (isBlank(?e) && 
    exists { ?x ?p ?e . filter (?x != ?r || ?x = ?e) }
    as ?refer)
}


function st:isWFL(?list){
   ( isBlank(?list)
   
     && exists { ?list rdf:rest* rdf:nil }
    
     && not exists {
        ?list rdf:rest* ?n
        
        filter (! ( 
            # every ?n must verify this:
            
            ?n = rdf:nil 
            
            # ouside the list
            || exists { rdf:nil rdf:rest* ?n }
            
            || (isBlank(?n)
            
            && exists {        
                ?n rdf:first ?f ; rdf:rest ?r 
                
                # nothing else than first|rest
                { select ?n (count(*) as ?c) where { ?n ?p ?v } group by ?n having (?c = 2) }
                    
                # ?n is first list node OR nobody else points to ?n
                filter (
                  #  ?n = ?list ||
                    exists { select ?n (count(*) as ?cc) where { ?s ?q ?n } group by ?n having (?cc = 1) }
                )

            }
            
            # no rdf:first in the list points to ?n
            && not exists { ?list rdf:rest*/rdf:first ?n }            
            )
        ))
    }
    )
}
