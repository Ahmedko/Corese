#
# SPARQL-based RDF AST Pretty-printer
# Olivier Corby, Wimmics Inria I3S, 2013
#
prefix sp:      <http://spinrdf.org/sp#> .

template {
  if (! (exists {?x ?p ?in}), st:prolog(), "") 
  st:call-template(sp:kw, "select") " " 
  if (bound(?t), st:call-template(sp:kw,"distinct "), "")
  if (bound(?r), "reduced ", "")
  if (bound(?select), 
    st:call-template(sp:list, ?select), 
    "*")  
  
  st:nl()
  st:call-template(sp:from, ?in)
  st:call-template(sp:named, ?in)
  
  st:call-template(sp:kw, "where") " {" 
      box { 
	st:call-template(sp:listnl, ?where) 
      } 
  "}" 
  
  st:nl()
  
  ?group
  ?order
  ?having 
  
  if (bound(?l), 
  concat(st:call-template(sp:kw, "limit"),  " ", ?l, st:nl()), "")  
  if (bound(?o), 
  concat(st:call-template(sp:kw, "offset"), " ", ?o, st:nl()), "") 
  
  st:call-template(sp:values, ?in)
}
where {
  ?in a sp:Select
  ?in sp:where ?where
  
  optional { ?in sp:resultVariables ?select }
  optional { ?in sp:distinct ?t filter(?t = true) }    
  optional { ?in sp:reduced  ?r filter(?r = true) }  

  optional { ?in sp:groupBy  ?group }
  optional { ?in sp:orderBy  ?order }
  optional { ?in sp:having   ?having }
  optional { ?in sp:limit  ?l }
  optional { ?in sp:offset ?o }
}



