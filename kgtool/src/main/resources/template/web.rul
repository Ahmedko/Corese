<?xml version='1.0' encoding='UTF-8'?>
<!--
SPARQL Template Transformation
Olivier Corby - Wimmics - Inria UNS CNRS I3S
Fri Jan 23 10:56:40 CET 2015
-->
<rdf:RDF  xmlns:rdf='http://www.w3.org/1999/02/22-rdf-syntax-ns#' 
  xmlns='http://ns.inria.fr/edelweiss/2011/rule#'>
<rule>
<body>
<![CDATA[
#
# Generate a form with a SPARQL query ?q to submit
# Argument uri ?t is the name of query (st:minus)
#
template st:form(?t, ?q, ?s) {
  """
  <script type="text/javascript">
    
        function showHide()
        {
            if(document.getElementById('cb').checked)
            {
                document.getElementById('sol').style.visibility = 'visible';
            }
            else
            {
                document.getElementById('sol').style.visibility = 'hidden';
            }
        }

    </script>
  
  
  
  <table>
  <tr>
  <td>
   <form method='post' id='exo' action='' >
       
   <input type='submit' value='Submit'/>
   """
   if (?s != st:null, 
     """<input id="cb" type="checkbox" onclick="showHide();"  />
     <label for="cb">Show solution</label>""", "")
    
   """
   <input type='hidden' name='profile' value='""" st:get(st:profile) """'/>
   
   <input type='hidden' name='uri' value='""" ?t """'/>
   
   <br/>
    <textarea cols='50' rows='10' name='query'>"""str(?q)
    
  """</textarea>
  </form>
  </td>
  
  <td>
   
   <div id='sol' action='' style='visibility:hidden'>
    <textarea cols='50' rows='10' name='query'>"""str(?s)
    
  """</textarea>
   </div>
  
  </td>
  </tr>
  </table>
  """
}
where {

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# SPARQL Tutorial
# st:get(st:uri) is the name of a query to execute (st:minus)
# It is used 1) to select a query and 2) to display the result of the query
# RDF graph is either RDF dataset or Query Result
# Query definitions are stored in st:context named graph 
#
template st:start {

  st:call-template(st:head)
  
  st:call-template(st:select, ?uri)
  
  st:call-template(st:display, ?uri)
   
  st:call-template(st:tail)

}
where {

  bind (coalesce(st:get(st:uri), st:null) as ?uri)
  
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Select a query name to display and execute
# query name bound to uri param
# Generate query names list from context graph
#
template st:select(?t) {

"""
<h3>Select a query</h3>

<form method='get' action=''>

  <input type='hidden' name='profile' value='""" st:get(st:profile) """' />
"""

  "<select name='uri' onChange='submit()'>"
  
    st:call-template(st:list, ?t)  
    
  "</select>"
  
  
""" 
</form>
"""

}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:content {
  group distinct { "<a href='" str(?d) "'>RDF Dataset</a>" } " - "
  group distinct { "<a href='" str(?s) "'>RDFS Vocabulary</a>" } " - "
  group distinct { "<a href='" str(?c) "'>SPARQL Queries</a>" }
  
}
where {
  bind(st:get(st:context) as ?g)
  graph ?g {
    optional { [] st:data    [ st:uri ?d ] }
    optional { [] st:schema  [ st:uri ?s ] }
    optional { [] st:context [ st:uri ?c ] }
  }
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# ?n is the st:name of a query
# get definition of query in context graph
# display query and eventually result of query
# st:get(st:query) is user query
# ?q is tutorial query
#
template st:display(?n) {

"<h3>" str(?l) "</h3>"
"<p>"  
  str(?c) 
  if (bound(?r), 
    concat("<br/><a href='", str(?r), "'>(", str(?r), ")</a>"), "")
"</p>"

   st:call-template(st:form, ?n,
     coalesce(st:get(st:query), str(?q)), coalesce(?s, st:null))
   
   st:call-template(st:result)
      
}
where {
  # named graph that contains definition of queries
  bind (st:get(st:context) as ?g)
  graph ?g {
    ?qt st:name    ?n ;
      rdfs:label   ?l ;
      rdfs:comment ?c ;
      st:query     ?q
      optional { ?qt st:reference ?r }
      optional { ?qt st:solution ?s }
      filter(lang(?c) = (coalesce(st:get(st:lang), "fr")))
  }
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Generate query names list from context graph
#
template st:list(?t) {

  "<option value='" ?q "'" 
    if (?q = ?t, "selected='true'", "")
  ">" 
  st:number() ". " str(?title) 
  "</option>" 
   
}
where {
  bind (st:get(st:context) as ?g)
  graph ?g {
    [] a st:Query ;
      st:name ?q ;
      rdfs:label ?title
  }
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:tail {
  
  """  
  <p>
  Generated by <a href='http://ns.inria.fr/sparql-template'>SPARQL Template Transformation</a> using <a href='http://wimmics.inria.fr'>Corese</a>
  """
  
  now()
  
  """
  </p>
  
  </body>
  </html>
  """

}
where {

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:banner  {
"""
<table style="width:100%">
<tr>
<td><a href='http://www.inria.fr/centre/sophia'><img alt ='INRIA' src='http://www.inria.fr/var/inria/storage/images/medias/inria/images-corps/logo-inria-scientifique-couleur/404505-4-fre-FR/logo-inria-scientifique-couleur.jpg'/></a></td>

<td><a href='http://wimmics.inria.fr'><img alt='Wimmics' src ='/img/wimmics.png' height='100'/></a></td>

<td><a href='http://www.i3s.unice.fr'><img alt='I3S' src ='http://www.i3s.unice.fr/I3S/LOGOS/NewlogoI3S-250x70.png' /></a></td>
</tr>
</table>
"""
}
where {}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:head {
  """
  <html>
  <head>
    <title>Corese Semantic Web Server</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
    <link href="/style.css" rel="stylesheet" type="text/css" />

    <style type='text/css'>
	.s0 { background:lightgrey }
	.s1 { background:lightblue }
    </style>
    
  </head>
  <body>
  """
  st:call-template(st:banner)
  
  "<h2>SPARQL Tutorial</h2>"
  
  st:call-template(st:content)
}
where {

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Display result of query
# RDF graph is a result graph
#

template st:result {

   st:call-template-with(st:sparql, st:result, st:get(st:service), st:sparql)
    
}
where {
  bind (st:get(st:query) as ?query)
  filter bound(?query)
}
]]>
</body>
</rule>

</rdf:RDF>
