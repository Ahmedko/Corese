<?xml version='1.0' encoding='UTF-8'?>
<!--
SPARQL Template Transformation
Olivier Corby - Wimmics - Inria UNS CNRS I3S
Tue Feb 03 16:35:53 CET 2015
-->
<rdf:RDF  xmlns:rdf='http://www.w3.org/1999/02/22-rdf-syntax-ns#' 
  xmlns='http://ns.inria.fr/edelweiss/2011/rule#'>
<rule>
<body>
<![CDATA[
#
# Generate HTML from SPARQL Query Results
# RDF is either construct where graph or 
# select where in W3C DAWG result-set RDF vocabulary
#
# Olivier Corby - Wimmics Inria I3S - 2015
#

template st:start {

  st:call-template(st:head)
  
  st:call-template(st:query, coalesce(st:get(st:query), ""))
  
  st:call-template(st:page)
    
  st:call-template(st:tail)

}
where {


}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
prefix rs: <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>
template st:th {
 
    "<th>" str(?v) "</th>" ; separator = " "
 
}
where {
  ?x rs:resultVariable ?v
}
order by ?v
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:link(?uri, ?title) {
   "<a href=\""  str(?uri) "\">" str(?title) "</a>"
}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Utility template to be used by another transformation to
# integrate SPARQL Query Results without headings
#
template st:result(?service, ?profile) {
   
  st:call-template(st:page)
      
}
where {
  bind (st:set(st:service, coalesce(?service, "/template")) as ?s)
  bind (st:set(st:profile, coalesce(?profile, st:sparql)) as ?p)
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
prefix rs: <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>
template st:table {

 "<tr class='s" 
   if (kg:even(st:number()), 1, 0)
 "'>"
 
  "<th>"  st:number() "</th>"
  
    st:apply-templates(?s)
 
 "</tr>"
 
}
where {
  ?x rs:solution ?s 
  ?s rs:index ?i
  
}
order by ?i
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:display(?x) {
  if (! bound(?profile), 
    st:call-template(st:esc, ?x),
    if (?profile = st:sparql, 
      st:call-template(st:linkprofile, ?x),
      st:call-template(st:linkesc, ?x)))
          
}
where {
      bind (st:get(st:profile) as ?profile)
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Trap select * where {} solution as there is no variable (see solution.rq)
#
template st:default {

}
where {

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
prefix rs: <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>

template st:pageask {
"""
  <h3>Result</h3>
  <p>
  """      
  ?b  
  "</p>"
}
where {
  ?r rs:boolean ?b
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:linkprofile(?x) {

  if (isURI(?x), 
    st:call-template(st:link, 
      coalesce(st:call-template(st:transform, ?x), encode_for_uri(?x)),
      ?e),
    str(?e))
    
}
where {

  bind (st:call-template(st:esc, ?x) as ?e)

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:transform(?x) {

    st:get(st:service) "?uri=" encode_for_uri(?x) "&profile=" st:get(st:profile)
    
}
where {

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:pageselect {
   
  st:call-template(st:open)
  
  st:call-template(st:table)
  
  st:call-template(st:close)
    
}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:close {
  
  "</table>"
   
}
where {

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:query(?q) {
  """
  <h3>Query</h3>
  <form method='post' action='"""
    
  st:get(st:service)
  
  """'>
  
   <select name="profile">
    <option value="st:sparql">LDP</option>
    <option value="st:std">STD</option>
   </select> 
  
    <br/>
    <textarea cols='60' rows='10' name='query'>"""str(?q)
    
  """</textarea>
    <br/>
    <input type='submit' value='Submit'/>
  </form>
  """
}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:tail {
  
  """  
  <p>
  Generated by <a href='http://ns.inria.fr/sparql-template'>SPARQL Template Transformation</a> using <a href='http://wimmics.inria.fr'>Corese</a>
  """
  
  now()
  
  """
  </p>
  
  </body>
  </html>
  """

}
where {

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:opencons {

  """
  <h3>Result</h3>
  <table>
  <tr class='s1'>
  <th></th><th></th><th>subject</th><th>property</th><th>object</th><th></th>
  </tr>
  """
  
}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:banner  {
"""
<table style="width:100%">
<tr>
<td><a href='http://www.inria.fr/centre/sophia'><img alt ='INRIA' src='http://www.inria.fr/var/inria/storage/images/medias/inria/images-corps/logo-inria-scientifique-couleur/404505-4-fre-FR/logo-inria-scientifique-couleur.jpg'/></a></td>

<td><a href='http://wimmics.inria.fr'><img alt='Wimmics' src ='/img/wimmics.png' height='100'/></a></td>

<td><a href='http://www.i3s.unice.fr'><img alt='I3S' src ='http://www.i3s.unice.fr/I3S/LOGOS/NewlogoI3S-250x70.png' /></a></td>
</tr>
</table>
"""
}
where {}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
prefix rs: <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>
template st:decl {

  """
  <h3>Result</h3>
  <table>
  """
  
 "<tr class='s1'><td/>"
 
  st:call-template(st:th)
  
 "</tr>\n"
}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
prefix rs: <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>

template st:page {
   
  st:call-template(?page)
      
}
where {
  bind (
    if (exists { ?x a rs:ResultSet }, 
      if (exists { ?x rs:boolean ?b }, st:pageask, st:pageselect),
      st:pagecons) 
    as ?page)
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:pagecons {
   
  st:call-template(st:opencons)
  
  st:call-template(st:tablecons)
  
  st:call-template(st:close)
    
}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:esc(?x) {

  str(?tt)
    
}
where {

   bind(st:turtle(?x) as ?t)
   
   bind (replace(?t, "<", "&lt;") as ?tt)
   
   bind(
    if (isURI(?x) && strstarts(?t, "<"), str(?x), ?t) 
    as ?d) 
    
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
#  Get an optional icon from the dataset and display it
#
template st:show(?x) {
    "<img width='35' src='"  str(?l)   "' />" 
}
where {
  bind (st:get(st:dataset) as ?g)
  graph ?g { 
    { ?x st:icon ?l }  
    union { ?x a ?t . ?t st:icon ?l }  
  }
}
limit 1
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
prefix rs: <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>
template st:open {

  """
  <h3>Result</h3>
  <table>
  """
  
 "<tr class='s1'><td/>"
 
  st:call-template(st:th)
  
 "</tr>\n"
}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
#  Get an optional icon from the dataset and display it
#
template st:show2(?x) {
    st:call-template(st:display, ?y) 
}
where {
  bind (st:get(st:dataset) as ?g)
  graph ?g { 
     ?x rdfs:seeAlso ?y   
  }
}
limit 1
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:head {
  """
  <html>
  <head>
    <title>Corese Semantic Web Server</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
    <link href="/style.css" rel="stylesheet" type="text/css" />

    <style type='text/css'>
	.s0 { background:lightgrey }
	.s1 { background:lightblue }
    </style>
    
  </head>
  <body>
  """
  st:call-template(st:banner)
  
  
  
}
where {

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:linkesc(?x) {

  if (isURI(?x), 
    st:call-template(st:link, ?x, ?e),
    str(?e))
    
}
where {

  bind (st:call-template(st:esc, ?x) as ?e)
    
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:tablecons {

 "<tr class='s" 
   if (kg:even(st:number()), 1, 0)
 "'>"
 "<th>"      st:number()
 "</th><td>" st:call-template(st:show,    ?x) 
 "</td><td>" st:call-template(st:display, ?x) 
 "</td><td>" st:call-template(st:display, ?p) 
 "</td><td>" st:call-template(st:display, ?y) 
 "</th><td>" st:call-template(st:show,    ?y) 
 "</td>"
 "</tr>"
 
}
where {
  ?x ?p ?y
  bind (st:get(st:uri) as ?uri)
  filter (if (bound(?uri), ?uri in (?x, ?p, ?y), true))
}
order by ?x ?p ?y
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
prefix rs: <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>
template  {
 
    "<td>"  
    coalesce(st:call-template(st:display, ?val), "&nbsp;")
    "</td>" ; separator = " "
  
}
where {
  ?x rs:solution ?in
  ?x rs:resultVariable ?var 
  
  optional {
    ?in rs:binding [ rs:variable ?var ; rs:value ?val ]
  }
  
}
order by ?var
]]>
</body>
</rule>

</rdf:RDF>
