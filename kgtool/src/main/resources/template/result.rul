<?xml version='1.0' encoding='UTF-8'?>
<!--
SPARQL Template Transformation
Olivier Corby - Wimmics - Inria UNS CNRS I3S
Fri Dec 05 10:22:58 CET 2014
-->
<rdf:RDF  xmlns:rdf='http://www.w3.org/1999/02/22-rdf-syntax-ns#' 
  xmlns='http://ns.inria.fr/edelweiss/2011/rule#'>
<rule>
<body>
<![CDATA[
template st:start {

  st:call-template(st:head)
  
  st:call-template(st:decl)
  
  st:call-template(st:table)
  
  st:call-template(st:tail)

}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:query {
  """
  <h3>Query</h3>
  <form method='post' action='"""
    
  st:get(st:service)
  
  """'>
  
   <select name="profile">
    <option value="st:ldp">LDP</option>
    <option value="st:result">STD</option>
   </select> 
  
    <br/>
    <textarea cols='60' rows='15' name='query'>"""

    st:get(st:query)
    
  """</textarea>
    <br/>
    <input type='submit' value='Submit'/>
  </form>
  """
}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
prefix rs: <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>
template st:th {
 
    "<th>" str(?v) "</th>" ; separator = " "
 
}
where {
  ?x rs:resultVariable ?v
}
order by ?v
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:tail {
  
  """
  </table>
  
  <p>
  Generated by <a href='http://ns.inria.fr/sparql-template'>SPARQL Template Transformation</a> using <a href='http://wimmics.inria.fr'>Corese</a>
  """
  
  now()
  
  """
  </p>
  
  </body>
  </html>
  """

}
where {

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
prefix rs: <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>
template st:decl {
 "<tr class='s1'><td/>"
 
  st:call-template(st:th)
  
 "</tr>\n"
}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:link(?uri, ?title) {
   "<a href=\""  str(?uri) "\">" str(?title) "</a>"
}
where {
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
prefix rs: <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>
template st:table {

 "<tr class='s" 
   if (kg:even(st:number()), 1, 0)
 "'>"
 
  "<th>"  st:number() "</th>"
  
    st:apply-templates(?s)
 
 "</tr>"
 
}
where {
  ?x rs:solution ?s 
  ?s rs:index ?i
  
}
order by ?i
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:esc(?x) {

  str(?d)
    
}
where {

   bind(st:turtle(?x) as ?t)
   
   bind(
    if (isURI(?x) && strstarts(?t, "<"), str(?x), ?t) 
    as ?d) 
    
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:display(?x) {

  if (! bound(?profile), 
    st:call-template(st:esc, ?x),
    if (?profile = st:ldp, 
      st:call-template(st:linkprofile, ?x),
      st:call-template(st:linkesc, ?x)))
          
}
where {
      bind (st:get(st:profile) as ?profile)
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:head {
  """
  <html>
  <head>
    <title>SPARQL Result</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
    
    <style type='text/css'>
	.s0 { background:lightgrey }
	.s1 { background:lightblue }
    </style>
    
  </head>
  <body>
  """
  
  st:call-template(st:query)
  
  """
  <h3>Result</h3>
  <table>
  """
}
where {

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:linkesc(?x) {

  if (isURI(?x), 
    st:call-template(st:link, ?x, ?e),
    str(?e))
    
}
where {

  bind (st:call-template(st:esc, ?x) as ?e)
    
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:linkprofile(?x) {

  if (isURI(?x), 
    st:call-template(st:link, st:call-template(st:transform, ?x), ?e),
    str(?e))
    
}
where {

  bind (st:call-template(st:esc, ?x) as ?e)

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
template st:transform(?x) {

    st:get(st:service) "?uri=" encode_for_uri(?x) "&profile=" st:get(st:profile)
    
}
where {

}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
prefix rs: <http://www.w3.org/2001/sw/DataAccess/tests/result-set#>
template  {
 
    "<td>"  
    coalesce(st:call-template(st:display, ?val), "&nbsp;")
    "</td>" ; separator = " "
  
}
where {
  ?x rs:solution ?in
  ?x rs:resultVariable ?var 
  
  optional {
    ?in rs:binding [ rs:variable ?var ; rs:value ?val ]
  }
  
}
order by ?var
]]>
</body>
</rule>

</rdf:RDF>
